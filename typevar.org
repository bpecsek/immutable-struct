* macro define-with-typevar ((typevars) &body body)

Defines some type variables and applies a special method to treat
=defstruct,ftype,defun= forms inside the body.  It supports a trivial type
propagation mechanism.  Thanks to the static typing, it results in a fast
and efficient machine code unlike CLOS.

Consider the following example:

#+BEGIN_SRC lisp
(define-with-typevar (<s> <t>)
  (defstruct coordinate2
    (x 0 :type <s>)
    (y 0 :type <t>)
    (z 0 :type <s>))
  (ftype vector-+ (/ coordinate2 <s> <t>) (/ coordinate2 <s> <t>) (/ coordinate2 <s> <t>))
  (defun vector-+ (v1 v2)
    (declare (optimize (speed 3) (space 0) (safety 0) (debug 0)))
    (coordinate2
     (+ (coordinate2-x v1)
        (coordinate2-x v2))
     (+ (coordinate2-y v1)
        (coordinate2-y v2))
     (+ (coordinate2-z v1)
        (coordinate2-z v2)))))
#+END_SRC

The instantiation of the structures/functions can be achived later, with
functions =instantiate-structure= and =instantiate-ftype=.
These functions defines the specialized instances of
those functions/structures. New instances are named as =<original name>{/<type>}*=.

#+BEGIN_SRC lisp
(instantiate-ftype 'vector-+ 'fixnum 'fixnum)

;-->
; Instantiating a function VECTOR-+ with typevars (FIXNUM FIXNUM)
; (FTYPE VECTOR-+/FIXNUM/FIXNUM (/ COORDINATE2 FIXNUM FIXNUM) (/ COORDINATE2 FIXNUM FIXNUM)...)
; Instantiating a structure COORDINATE2 with typevars (FIXNUM FIXNUM)..
#+END_SRC

Unlike CLOS, the resulting function is statically typed, fast and efficent.

#+BEGIN_SRC lisp
; disassembly for VECTOR-+/FIXNUM/FIXNUM
; Size: 84 bytes. Origin: #x100C2CD232
; 32:       4C8B410D         MOV R8, [RCX+13]                 ; no-arg-parsing entry point
; 36:       488B470D         MOV RAX, [RDI+13]
; 3A:       49D1F8           SAR R8, 1
; 3D:       48D1F8           SAR RAX, 1
; 40:       4901C0           ADD R8, RAX
; 43:       4C8B4915         MOV R9, [RCX+21]
; 47:       488B4715         MOV RAX, [RDI+21]
; 4B:       49D1F9           SAR R9, 1
; 4E:       48D1F8           SAR RAX, 1
; 51:       4901C1           ADD R9, RAX
; 54:       488B711D         MOV RSI, [RCX+29]
; 58:       488B471D         MOV RAX, [RDI+29]
; 5C:       48D1FE           SAR RSI, 1
; 5F:       48D1F8           SAR RAX, 1
; 62:       4801C6           ADD RSI, RAX
; 65:       49D1E0           SHL R8, 1
; 68:       49D1E1           SHL R9, 1
; 6B:       48D1E6           SHL RSI, 1
; 6E:       498BD0           MOV RDX, R8
; 71:       498BF9           MOV RDI, R9
; 74:       488B0565FFFFFF   MOV RAX, [RIP-155]               ; #<FDEFINITION for COORDINATE2/FIXNUM/FIXNUM>
; 7B:       B906000000       MOV ECX, 6
; 80:       FF7508           PUSH QWORD PTR [RBP+8]
; 83:       FF6009           JMP QWORD PTR [RAX+9]
#+END_SRC

The type specifier for the instantiated structures can be referenced by =(/
lifted args...)=.  The other aspect of this type is that the type
/propagates/.  Upon the type expansion, it tries to instantiate the
structure instance if it is not already instantiated.

#+BEGIN_SRC lisp
(deftype / (name &rest args)
   (or (gethash (cons name args) *typevar-types*)
       (progn
         (apply #'instantiate-structure name args)
         (gethash (cons name args) *typevar-types*))
       (error "failed to expand the lifted structure type ~a with arguments ~a" name args)))
#+END_SRC
